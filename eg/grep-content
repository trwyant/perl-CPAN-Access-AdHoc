#!/usr/bin/env perl

use 5.010;

use strict;
use warnings;

use CPAN::Access::AdHoc;
use Getopt::Long 2.33 qw{ :config auto_version };
use Pod::Usage;

our $VERSION = '0.000_216';

my %opt;

GetOptions( \%opt,
    help => sub { pod2usage( { -verbose => 2 } ) },
) and @ARGV or pod2usage( { -verbose => 0 } );

my $cad = CPAN::Access::AdHoc->new();

my $re = qr< @{[ join ' | ', @ARGV ]} >smx;

my $author_index = $cad->fetch_author_index();

foreach my $author ( sort keys %{ $author_index } ) {
    my @corpus = eval {
	$cad->corpus( $author, latest => 1 ),
    } or next;
    foreach my $distro ( sort @corpus ) {
	my $archive = $cad->fetch_distribution_archive( $distro );
	foreach my $item ( $archive->list_contents() ) {
	    my $content = $archive->get_item_content( $item );
	    $content =~ $re
		or next;
	    say "$distro $item";
	}
    }
}

__END__

=head1 TITLE

grep-content - Search CPAN for a given string

=head1 SYNOPSIS

 grep-content '\bFubar\b'
 grep-content -help
 grep-content -version

=head1 OPTIONS

=head2 -help

This option displays the documentation for this script. The script then
exits.

=head2 -version

This option displays the version of this script. The script then exits.

=head1 DETAILS

This Perl script searches all of CPAN for files that match any of one or
more regular expressions. When a match occurs the name of the
distribution and the file in the distribution are written to standard
out.

Note that the arguments are regular expressions, not strings. The actual
matching is done by a single regular expression which is the logical
'or' of the arguments. This expression is compiled under C</smx>, so if
you want to match white space you will need to escape it, quote it with
C<\Q...\E>, or use C<\s>. This also means C<'#'> introduces comments
unless escaped.

=head1 AUTHOR

Tom Wyant (wyant at cpan dot org)

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2016-2020 by Thomas R. Wyant, III

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl 5.10.0. For more details, see the full text
of the licenses in the directory LICENSES.

This program is distributed in the hope that it will be useful, but
without any warranty; without even the implied warranty of
merchantability or fitness for a particular purpose.

=cut

# ex: set textwidth=72 :
